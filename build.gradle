buildscript {
    apply from: './dependencies.gradle'

    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVer"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:$ktlintPluginVer"
    }
}

plugins {
    id 'nebula.lint' version "$nebulaLintPluginVer"
    id 'com.gradle.build-scan' version "$buildscanPluginVer"
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

subprojects {
    repositories {
        jcenter()
    }

    apply plugin: 'kotlin'
    apply plugin: 'kotlin-kapt'
    apply plugin: 'nebula.lint'
    apply plugin: 'org.jlleitschuh.gradle.ktlint'

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVer"
    }

    ktlint {
        coloredOutput = false // Colors break source links in the IDEA console.
    }

    group 'org.wycliffeassociates'
    sourceCompatibility = 11

    compileKotlin {
        kotlinOptions.jvmTarget = "11"
        kotlinOptions.allWarningsAsErrors = true
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "11"
        kotlinOptions.allWarningsAsErrors = true
    }

    gradleLint.rules = ['deprecated-dependency-configuration']
}